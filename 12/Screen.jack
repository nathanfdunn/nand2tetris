// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Screen.jack

/**
 * A library of functions for displaying graphics on the screen.
 * The Hack physical screen consists of 512 rows (indexed 0..511, top to bottom)
 * of 256 pixels each (indexed 0..255, left to right). The top left pixel on 
 * the screen is indexed (0,0).
 */
class Screen {

    static boolean color;
    static int screenStart;
    // Note: this will be illegal
    static int screenEnd;
    static int maxScreenOffset;

    static Array twoToThe;

    /** Initializes the Screen. */
    function void init() {
        let screenStart = 16384;
        let maxScreenOffset = 8192;
        let screenEnd = screenStart + maxScreenOffset;
        let color = true;
        
        let twoToThe = Array.new(16);
     
        let twoToThe[0] = 1;
        let twoToThe[1] = 2;
        let twoToThe[2] = 4;
        let twoToThe[3] = 8;
        let twoToThe[4] = 16;
        let twoToThe[5] = 32;
        let twoToThe[6] = 64;
        let twoToThe[7] = 128;
        let twoToThe[8] = 256;
        let twoToThe[9] = 512;
        let twoToThe[10] = 1024;
        let twoToThe[11] = 2048;
        let twoToThe[12] = 4096;
        let twoToThe[13] = 8192;
        let twoToThe[14] = 16384;
        
        let twoToThe[15] = twoToThe[14] + twoToThe[14];

        return;
    }

    /** Erases the entire screen. */
    function void clearScreen() {
        var int temp;
        let temp = screenStart;
        while (temp < screenEnd) {
            let temp[0] = color;
            let temp = temp + 1;
        }
        return;
    }

    /** Sets the current color, to be used for all subsequent drawXXX commands.
     *  Black is represented by true, white by false. */
    function void setColor(boolean b) {
        let color = b;
        return;
    }

    // TODO put this in Math?
    function int modulo(int x, int mod) {
        return x - Math.multiply(mod, Math.divide(x, mod));
    }

    /** Draws the (x,y) pixel, using the current color. */
    function void drawPixel(int x, int y) {
        var int memoryLocation;
        var int bitMask;
        
        let memoryLocation = screenStart + Math.multiply(32, y) + Math.divide(x, 16);
        // let memoryLocation = 0;
        let bitMask = twoToThe[Screen.modulo(x, 16)];
        // let bitMask = 4095;

        if (color) {
            let memoryLocation[0] = memoryLocation[0] | bitMask; 
        }
        else {
            let memoryLocation[0] = memoryLocation[0] & ~bitMask; 
        }
        return;
    }

    /** Draws a line from pixel (x1,y1) to pixel (x2,y2), using the current color. */
    function void drawLine(int x1, int y1, int x2, int y2) {
        var int dx;
        var int dy;
        var int temp;
        var int a;
        var int b;
        var boolean isInverted;

        if (x1 > x2) {
            do Screen.drawLine(x2, y2, x1, y1);
            return;
        }

        // From this point on we can assume x1 <= x2

        let dx = x2 - x1;

        if (y1 < y2) {
            let dy = y2 - y1;
            let isInverted = false;
        }
        else {
            let dy = y1 - y2;
            let temp = y1;
            let y1 = y2;
            let y2 = temp;
            let isInverted = true;
        }

        // TODO optimize this
        if (dy = 0) {
            let x2 = x2 + 1;
            while (x1 < x2) {
                do Screen.drawPixel(x1, y1);
                let x1 = x1 + 1;
            }
            return;
        }

        let a = 0;
        let b = 0;

        while ((a < (dx +1)) &
                (b < (dy + 1)))
        {
            if (isInverted) {
                do Screen.drawPixel(x1 + a, y2 - b);
            }
            else {
                do Screen.drawPixel(x1 + a, y1 + b);
            }

            if (Math.multiply(a, dy) < Math.multiply(b, dx)) {
                let a = a + 1;
            }
            else {
                let b = b + 1;
            }
        }

        return;
    }

    /** Draws a filled rectangle whose top left corner is (x1, y1)
     * and bottom right corner is (x2,y2), using the current color. */
    function void drawRectangle(int x1, int y1, int x2, int y2) {
        // Think we can assume x1 < x2 and y1 < y2

        let y2 = y2 + 1;
        while (y1 < y2) {
            do Screen.drawLine(x1, y1, x2, y1);
            let y1 = y1 + 1;
        }


        // var int tempY;
        // // TODO optimize
        // let x2 = x2 + 1;
        // let y2 = y2 - 1;
        // while (x1 < x2) {
        //     let tempY = y1;
        //     while (tempY < y2) {
        //         do Screen.drawPixel(x1, tempY);
        //         let tempY = tempY + 1;
        //     }
        //     let x1 = x1 + 1;
        // }
        return;
    }

    /** Draws a filled circle of radius r<=181 around (x,y), using the current color. */
    function void drawCircle(int x, int y, int r) {
        var int dy;
        var int dx;
        let dy = -r;

        while (dy < (r + 1)) {
            let dx = Math.sqrt(Math.multiply(r, r) - Math.multiply(dy, dy));
            do Screen.drawLine(x - dx, y + dy, x + dx, y + dy);
            let dy = dy + 1;
        }

        return;
    }
}
